apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'java-test-fixtures'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    // JUnit
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
}

def spotlessEnabled = Boolean.getBoolean('enable.spotless')
if (spotlessEnabled) {
    apply plugin: "com.diffplug.spotless"
    spotless {
        java {
            licenseHeaderFile rootProject.file("spotless/HEADER.java")
            FileCollection files = files()
            for (SourceSet sourceSet : sourceSets)
                files = files.plus(sourceSet.allJava)
            target(files)
            removeUnusedImports()
            googleJavaFormat()
        }
        enforceCheck false
    }
}

def checkerFrameworkEnabled = Boolean.getBoolean('build.checker')
if (checkerFrameworkEnabled) {
    apply plugin: "org.checkerframework"
    dependencies {
        checkerFramework libs.checker
        implementation libs.checker.qual
    }
    checkerFramework {
        excludeTests = true
        // incrementalize = false
        checkers = [
                'org.checkerframework.checker.nullness.NullnessChecker',
                // 'org.checkerframework.checker.lock.LockChecker',
                'org.checkerframework.checker.optional.OptionalChecker',
                'org.checkerframework.checker.signedness.SignednessChecker',
                // 'org.checkerframework.checker.interning.InterningChecker',
                // 'org.checkerframework.checker.resourceleak.ResourceLeakChecker',
                // others at https://checkerframework.org/manual
        ]
        extraJavacArgs = [
                // enable these when cleaning up, disable them in CI to fail builds
                // '-Awarns', // when enabled, treats errors as warnings
                // '-Xmaxwarns', '10000',

                // '-AonlyDefs=^com.kwawingu', // enabling this hides errors we care about
                // '-AonlyUses=^com.kwawingu', // enabling this hides errors we care about
                '-AskipDefs=.*AutoValue_\\w+\$',
        ]
    }
}

def errorproneEnabled = Boolean.getBoolean('build.errorprone')
if (errorproneEnabled) {
    apply plugin: 'net.ltgt.errorprone'
    dependencies {
        errorprone libs.errorprone
    }
    tasks {
        compileTestFixturesJava {
            options.errorprone.enabled.set(false)
        }
        compileTestJava {
            options.errorprone.enabled.set(false)
        }
        compileJava {
            options.errorprone.disable("UnusedLabel", "LabelledBreakTarget") // labels often used as explanatory aids

            // converting common warnings to errors here in the absence of a way to escalate all warnings to errors
            options.errorprone.error(
                    "StringCaseLocaleUsage",
                    "AddressSelection"
            )

            // errorprone doesn't provide a way to escalate all warnings to errors
            // setting this javac option works, but at the cost of ignoring the --continue
            // flag we pass to gradle so as to continue to execute all other tasks before failing
            // options.compilerArgs << "-Werror" // when enabled, all warnings are errors
        }
    }
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
    testLogging {
        events "started", "passed", "skipped", "failed"
        if (Boolean.getBoolean("test.verbose")) {
            // events "passed", "skipped", "failed"
            showExceptions(true)
            exceptionFormat("full")
            showStandardStreams(true)
        }
        debug {
            events "started", "passed", "skipped", "failed", "standard_out", "standard_error"
            exceptionFormat "full"
        }
    }
}
